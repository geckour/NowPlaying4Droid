apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-android-extensions"

apply plugin: "kotlin-kapt"

apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlinx-serialization'

apply plugin: 'koin'

kapt.mapDiagnosticLocations = true

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.geckour.nowplaying4gpm"
        minSdkVersion 24
        versionCode 113
        versionName "2.13.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        dataBinding.enabled = true

        def filesAuthorityValue = applicationId + ".files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    applicationVariants.all {
        def keyNameLastFm = "LAST_FM_API_KEY"
        it.buildConfigField "String", keyNameLastFm, getKey(keyNameLastFm)
        def keyNameTwitterConsumerKey = "TWITTER_CONSUMER_KEY"
        it.buildConfigField "String", keyNameTwitterConsumerKey, getKey(keyNameTwitterConsumerKey)
        def keyNameTwitterConsumerSecret = "TWITTER_CONSUMER_SECRET"
        it.buildConfigField "String", keyNameTwitterConsumerSecret, getKey(keyNameTwitterConsumerSecret)
        def keyNameSkuDonate = "SKU_KEY_DONATE"
        it.buildConfigField "String", keyNameSkuDonate, getKey(keyNameSkuDonate)
        def keyNameMastodonInstancesSecret = "MASTODON_INSTANCES_SECRET"
        it.buildConfigField "String", keyNameMastodonInstancesSecret, getKey(keyNameMastodonInstancesSecret)
        def keyNameSpotifyClientId = "SPOTIFY_CLIENT_ID"
        it.buildConfigField "String", keyNameSpotifyClientId, getKey(keyNameSpotifyClientId)
        def keyNameSpotifyClientSecret = "SPOTIFY_CLIENT_SECRET"
        it.buildConfigField "String", keyNameSpotifyClientSecret, getKey(keyNameSpotifyClientSecret)
    }

    signingConfigs {
        debug {
            keyAlias "AndroidDebugKey"
            keyPassword "android"
            storeFile file("${project.rootDir}/app/signing/debug.keystore")
            storePassword "android"
        }
        release {
            def releaseSettingGradleFile = new File("${project.rootDir}/app/signing/release.gradle")
            apply from: releaseSettingGradleFile, to: android
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    dataBinding.enabled = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }


    lintOptions {
        checkReleaseBuilds false
    }
}

static def getKey(keyName) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File("secret.properties")))
    return "\"" + props[keyName] + "\""
}

dependencies {
    wearApp project(":wear")

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.google.android.material:material:1.5.0-alpha01"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    testImplementation "junit:junit:4.13.1"
    testImplementation "androidx.test:runner:1.4.0"
    testImplementation "androidx.test.ext:junit:1.1.3"
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "org.assertj:assertj-core:3.12.2"
    testImplementation "io.mockk:mockk:1.10.5"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    // Coroutines
    def coroutines_version = "1.5.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // DI
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    // Logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    // KTX
    implementation "androidx.core:core-ktx:1.6.0"
    implementation 'androidx.activity:activity-ktx:1.3.0-rc02'
    implementation 'androidx.fragment:fragment-ktx:1.4.0-alpha04'

    // JSON
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"

    // Permission
    implementation "org.permissionsdispatcher:permissionsdispatcher-ktx:1.0.1"

    // ViewModel
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Billing
    implementation "com.android.billingclient:billing:4.0.0"

    // Preference
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Palette API
    implementation "androidx.palette:palette-ktx:1.0.0"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"
    def okhttp_version = "4.9.0"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Image processing
    implementation "io.coil-kt:coil:1.1.1"

    // Wear
    implementation "com.google.android.gms:play-services-wearable:17.1.0"
    implementation "org.twitter4j:twitter4j-core:4.0.7"

    // Mastodon
    implementation "com.github.sys1yagi.mastodon4j:mastodon4j:1.7.0"
}

// Billing
apply plugin: "com.google.gms.google-services"