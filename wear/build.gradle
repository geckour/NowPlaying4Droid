plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.geckour.nowplaying4droid"
        targetSdkVersion 33
        minSdkVersion 25
        versionCode 330100105
        versionName "1.1.5"
        dataBinding.enabled = true

        def filesAuthorityValue = applicationId + ".files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    signingConfigs {
        debug {
            keyAlias 'AndroidDebugKey'
            keyPassword 'android'
            storeFile file("${project.rootDir}/app/signing/debug.keystore")
            storePassword 'android'
        }
        release {
            def releaseSettingGradleFile = new File("${project.rootDir}/app/signing/release.gradle")
            if (releaseSettingGradleFile.exists()) {
                apply from: releaseSettingGradleFile, to: android
            } else {
                throw new GradleException("Missing ${releaseSettingGradleFile.getAbsolutePath()} . Generate the file by copying and modifying ${project.rootDir}/app/signing/release.gradle.sample .")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }
    namespace 'com.geckour.nowplaying4droid'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly 'com.google.android.support:wearable:2.9.0'
    implementation 'com.google.android.gms:play-services-wearable:18.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.wear:wear:1.2.0'
    compileOnly 'com.google.android.wearable:wearable:2.9.0'
    implementation('com.google.firebase:firebase-core:21.1.1') {
        exclude group: 'com.google.android.gms', module: 'play-services-basement'
    }

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Compose
    implementation "androidx.activity:activity-compose:1.6.0"
    implementation "androidx.compose.compiler:compiler:1.3.2"
    implementation "androidx.compose.ui:ui:1.2.1"
    implementation "androidx.compose.ui:ui-tooling:1.2.1"
    implementation "androidx.compose.material:material:1.2.1"
    implementation "androidx.wear.compose:compose-foundation:1.1.0-alpha07"
    implementation "androidx.wear.compose:compose-material:1.1.0-alpha07"
    implementation "androidx.wear.tiles:tiles-material:1.1.0"
    implementation "com.google.android.horologist:horologist-tiles:0.1.14"
}
